#compdef _dots dots

# This is probably the shittiest completion file you can find.
# ------------------------------------------------------------------------------

_commands=(
  'install:Run the installation script for every topic'
  'symlink:Run the symlinking process'
  'list:List all topics'
)

_dots_commands() {
  _describe 'command' _commands
}

_dots() {
  local context state state_descr line
  typeset -A opt_args

  _arguments \
    '(-h --help)'{-h,--help}'[Show help output]' \
    '1: :_dots_commands' \
    '*:: :->command_args'

  case $state in
    command_args)
      case $words[1] in
        install)
          _dots_install
        ;;
        symlink)
          _dots_symlink
        ;;
        list)
          _dots_list
        ;;
      esac
    ;;
  esac
}

# ------------------------------------------------------------------------------

_dots_install() {
  _arguments -C \
    '(-y --yes)'{-y,--yes}'[Skip confirmation questions]' \
    '(-e --exclude)'{-e,--exclude}'[Exclude topics from installation]' \
    '(-d --debug-log)'{-d,--debug-log}'[Write commands output to debug log]' \
    '(-b --base-dir)'{-b,--base-dir}'[Set the dotfiles directory to run from]' \
    '*: :($(_get_runnable_topics))'
}

_dots_symlink() {
  _arguments -C \
    '(-D --dry-run)'{-D,--dry-run}'[Check the symlinking operations]'
}

_dots_list() {
  _arguments -C \
    '(-a --all)'{-a,--all}'[Show all topics]'
}

# ------------------------------------------------------------------------------

_get_runnable_topics() {
  find "$DOTFILES" \
    -mindepth 2 \
    -maxdepth 2 \
    -type f \
    -regex '.*/install\(.*\).sh' \
    -exec sh -c 'echo $(basename $(dirname {}))' \;
}

# ------------------------------------------------------------------------------
# vim:ft=zsh:
