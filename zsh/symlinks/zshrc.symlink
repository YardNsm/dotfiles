# --------------------------------------- #
# | Load stuff
# --------------------------------------- #

# Initialize autocomplete
autoload -U compinit && compinit

# Load Colors
autoload -U colors && colors
autoload -U promptinit && promptinit

# For globbing
setopt EXTENDED_GLOB

# --------------------------------------- #
# | Main Configurations
# --------------------------------------- #

# Core variables
export DOTFILES=$HOME/dotfiles
export ZSH=$DOTFILES/zsh
export CONFIG=$DOTFILES/_config

# Display info for tasks long than 8sec
export REPORTTIME=8

# --------------------------------------- #
# | Blox settings
# --------------------------------------- #

# For the spacing issue
BLOX_CHAR__SPACE=" "

# CWD block
BLOX_BLOCK__CWD_TRUNC=0

# Git block
BLOX_BLOCK__GIT_CLEAN_SYMBOL="✔︎%{$BLOX_CHAR__SPACE%}"
BLOX_BLOCK__GIT_DIRTY_SYMBOL="✘"
BLOX_BLOCK__GIT_UNPULLED_SYMBOL="⇣"
BLOX_BLOCK__GIT_UNPUSHED_SYMBOL="⇡"

# Battery block (macOS only)
function blox_block__macos_battery() {

  local per="${$(pmset -g batt | awk '{print $2}' | grep '%')%?}%"
  local stat="${$(pmset -g batt | awk '{print $3}' | grep 'char')}"
  local res=""

  local color=$fg_bold[yellow]
  local symb='♜'
  [[ $stat == 'discharging;' ]] && color=$fg[magenta] && symb='♖'

  # Build the block
  res+="%{$color%}"
  res+="${BLOX_CONF__BLOCK_PREFIX}${symb} ${per}${BLOX_CONF__BLOCK_SUFFIX}"
  res+="%{$reset_color%}"

  # Echo the block
  echo $res
}

# Structure
BLOX_SEG__UPPER_RIGHT='blox_block__bgjobs,blox_block__ruby,blox_block__nodejs,blox_block__time,blox_block__macos_battery'

# --------------------------------------- #
# | Sourcing stuff
# --------------------------------------- #

# Source all .zsh files within the dotfiles repo & exclude
# the non-topic folders (starts with `_`)
for config ($DOTFILES/(^_*/)#*.zsh); do
  source $config
done

# Source '~/.localrc' if exists
if [[ -a ~/.localrc ]]; then
  source ~/.localrc
fi

# --------------------------------------- #
# | Theme setup
# --------------------------------------- #

# Base16 Shell Theme
export THEME="base16-default"
if [ -z "$BACKGROUND" ]; then
  export BACKGROUND="dark"
fi
BASE16_SHELL="$CONFIG/base16-shell/$THEME.$BACKGROUND.sh"
[[ -s $BASE16_SHELL ]] && [ -n "$ITERM_SESSION_ID" ] && source $BASE16_SHELL
