# --------------------------------------- #
# | Load stuff
# --------------------------------------- #

# Initialize completion
autoload -U compinit && compinit

# Load Colors
autoload -U colors && colors
autoload -U promptinit && promptinit

# For globbing
setopt EXTENDED_GLOB

# --------------------------------------- #
# | Main Configurations
# --------------------------------------- #

# Core variables
export DOTFILES=$HOME/dotfiles
export ZSH=$DOTFILES/zsh
export CONFIG=$DOTFILES/_config

# Display info for tasks long than 8sec
export REPORTTIME=8

# --------------------------------------- #
# | Blox settings
# --------------------------------------- #

# CWD block
BLOX_BLOCK__CWD_TRUNC=0

# Host block
BLOX_BLOCK__HOST_USER_SHOW_ALWAYS=true
BLOX_BLOCK__HOST_MACHINE_SHOW_ALWAYS=true

# Battery block (macOS only)
function blox_block__macos_battery() {

  local per="${$(pmset -g batt | awk '{print $2}' | grep '%')%?}%"
  local stat="${$(pmset -g batt | awk '{print $3}' | grep 'char')}"
  local res=""

  local color=$fg[yellow]
  local symb='⇋'
  [[ $stat == 'discharging;' ]] && color=$fg[magenta] && symb='↼'

  # Build the block
  res+="%{$color%}"
  res+="${BLOX_CONF__BLOCK_PREFIX}${symb} ${per}${BLOX_CONF__BLOCK_SUFFIX}"
  res+="%{$reset_color%}"

  # Echo the block
  echo $res
}

# Structure
BLOX_SEG__UPPER_RIGHT=(blox_block__bgjobs blox_block__nodejs blox_block__macos_battery blox_block__time)

# --------------------------------------- #
# | Sourcing stuff
# --------------------------------------- #

# Source omz
#source $DOTFILES/oh-my-zsh/shell/init.zsh-before

# Source all .zsh files within the dotfiles repo & exclude
# the non-topic folders (starts with `_`)
for config ($DOTFILES/(^_*/)#*.zsh); do
  #[[ -z $PR_ZSH_SOURCED ]] && printf "\e[1;37msourcing\e[0m \e[0;90m$config\e[0m\n"
  source $config
done

# Clear logs
[[ -z $PR_ZSH_SOURCED ]] && clear
PR_ZSH_SOURCED=true

# Source '~/.localrc' if exists
if [[ -a ~/.localrc ]]; then
  source ~/.localrc
fi

# --------------------------------------- #
# | Theme setup
# --------------------------------------- #

# Base16 Shell Theme
export THEME="base16-3024"
if [ -z "$BACKGROUND" ]; then
  export BACKGROUND="dark"
fi
BASE16_SHELL="$CONFIG/base16-shell/$THEME.$BACKGROUND.sh"
[[ -s $BASE16_SHELL ]] && [ -n "$ITERM_SESSION_ID" ] && source $BASE16_SHELL
